DS is a language for writing game logic for scripting engines. This logic is
written as a series of expressions. The meaning of operators as well as their
precedence generally follows C rules.



arglist = "(" [ type [ identifier ] [ "," type [ identifier ] ]... ] ")"
arglist_return = arglist [ "->" type ]

ocode_set = "__ocode" "(" identifier [ "," identifier ] ")"



block {}
  Groups a set of expressions with a new context. Yields contained expressions.
  Can be cast to a struct or array.
    "{" [ expression ";" ]... "}"

block []
  Groups a set of expressions without a new context. Yields contained
  expressions. Can be cast to a struct or array.
    "[" [ expression ";" ]... "]"



operator ++
  Increments the value at the address of expression. Yields the new value for
  the first form and the old value for the second form.
    "++" expression_single
    expression_single "++"

operator --
  Decrements the value at the address of expression. Yields the new value for
  the first form and the old value for the second form.
    "--" expression_single
    expression_single "--"

operator ()
  Calls the expression with provided arguments, if any. Yields the result of the
  call. Creates a new context for expressions based on current context.
    expression_single "(" [ expression [ "," expression ]... ] ")"

operator []
  Yields an element of expression.
    expression_single "[" expression "]"

operator .
  Yields a member of expression.
    expression_single "." identifier

operator ->
  Yields a pointed-to member of expression.
    expression_single "->" identifier

operator + (unary)
  Yields the result of expression.
    "+" expression_single

operator - (unary)
  Yields the negative result of expression.
    "-" expression_single

operator !
  Yields the boolean inverse of expression.
    "!" expression_single

operator ~
  Yields the bitwise inverse of expression.
    "~" expression_single

operator type
  Yields the expression converted to type.
    "(" type ")" expression_single

operator * (unary)
  Yields the value pointed-to by expression.
    "*" expression_single

operator @
  Yields the "storage area" of expression. This is most useful for
  registerarray variables as it returns the array number.
    "@" expression_single

operator & (unary)
  Yields a pointer to expression.
    "&" expression_single

operator && (unary)
  Yields a pointer to label or case.
    "&&" "case" "default"
    "&&" "case" expression_single
    "&&" identifier

operator sizeof
  Yields the size of a type or expression.
    "sizeof" expression_single
    "sizeof" "(" type ")"

operator * (binary)
    expression "*" expression_single

operator /
    expression "/" expression_single

operator %
    expression "%" expression_single

operator + (binary)
    expression "+" expression_single

operator - (binary)
    expression "-" expression_single

operator >>
    expression ">>" expression_single

operator <<
    expression "<<" expression_single

operator >
    expression ">" expression_single

operator >=
    expression ">=" expression_single

operator <
    expression "<" expression_single

operator <=
    expression "<=" expression_single

operator ==
    expression "==" expression_single

operator !=
    expression "!=" expression_single

operator & (binary)
    expression "&" expression_single

operator ^ (binary)
    expression "^" expression_single

operator | (binary)
    expression "|" expression_single

operator && (binary)
    expression "&&" expression_single

operator ^^
    expression "^^" expression_single

operator ||
    expression "||" expression_single

operator ?:
  Performs a conditional branch. Yields the result of if-branch or else-branch.
  If else-branch is present, it must have the same type as if-branch. Otherwise,
  if-branch must have type void. Creates a new context for if-branch and
  else-branch based on current context.
    expression "?" expression_single ":" expression_single

operator =
  Assigns the result of the right expression to the address of the left
  expression.
    expression "=" expression

operator +=
    expression "+=" expression

operator -=
    expression "-=" expression

operator *=
    expression "*=" expression

operator /=
    expression "/=" expression

operator %=
    expression "%=" expression

operator <<=
    expression "<<=" expression

operator >>=
    expression ">>=" expression

operator &=
    expression "&=" expression

operator ^=
    expression "^=" expression

operator |=
    expression "|=" expression

operator &&=
    expression "&&=" expression

operator ^^=
    expression "^^=" expression

operator ||=
    expression "||=" expression

operator :
  Defines a jump target for a goto. Yields the result of expression.
    identifier ":" expression

operator asmfunc
  Defines a function executed as a single instruction. Yields a pointer to the
  function, which is a void type.
    "__asmfunc" type name arglist "@" ocode_set
    "__asmfunc" name arglist_return "@" ocode_set

operator break
  Branches out of the current loop or switch. Yields void.
    "break"

operator case
  Defines a jump target for a switch. Yields the result of expression. Must be
  possible to resolve case statically.
    "case" expression(case) ":" expression

operator constexpr
  Defines a named and typed constant. Yields the constant.
    "constexpr" type name "=" expression

operator continue
  Branches to the end of the current iteration of the current loop. Yields void.
    "continue"

operator default
  Defines the default jump target for a switch. Yields the result of expression.
    "default" ":" expression

operator delay
  Delays execution for a specified number of clocks (sometimes referred to tics
  or ticks). Yields void.
    "__delay" expression

operator do
  Performs iteration. Yields void. Creates new contexts for condition and body
  based on current context.
    "do" expression_single "while" "(" expression ")"

operator extern
  Performs forward-declaration. Yields the thing declared.
    "extern" "__function" name arglist_return
    "extern" "__script" name arglist_return
    "extern" "__variable" storage_class type name

operator for
  Performs iteration. Yields void. Creates a new context for initializer based
  on current context, a new context for condition based on initializer context,
  and a new context for remaining expressions based on condition context.
    "for" "(" expression ";" expression ";" expression ")" expression_single

operator function
  Defines a function. Yields a pointer to the function. Creates a new context
  for expression based on current context. The first form makes a function that
  can be exposed globally using extern.
    "__extfunc"  name arglist_return expression_single
    "__function" name arglist_return expression_single

operator goto
  Branches to a label or case. Yields void.
    "goto" "case" "default"
    "goto" "case" expression_single
    "goto" identifier

operator goto_dyn
  Branches to a label-pointer. Yields void.
    "__goto_dyn" expression_single

operator if
  Performs a conditional branch. Yields the result of if-branch or else-branch.
  Yields a value of the promoted type of the two expressions or void if there is
  no such promotion possible. Creates a new context for condition based on
  current context, and new contexts for remaining expressions based on condition
  context.
    "if" "(" expression ")" expression_single [ "else" expression_single ]

operator library
  Sets the current ACSE library. Yields void.
    "__library" "(" string ")"

operator linespec
  Defines a function executed as a line special. Yields a pointer to the
  function.
    "__linespec" name arglist_return @ expression_single

operator native
  Defines a function executed as a native function. Yields a pointer to the
  function.
    "__native" name arglist_return @ expression_single

operator output
  Prints expression for debugging. Yields void.
    "__output" expression

operator printf
  Prints a formatted string. Expressions are implicitly cast to the necessary
  type. The identifier indicates the type of printing operation and defaults to
  __printf_print. Some operations require additional expressions which must come
  after conversion expressions. Additionally, hud operations have a differing
  number of arguments based on the msgtype. Consequently, that field must be a
  compile-time constant. Yield depends on operation.
  Supported conversion specifiers:
   %K (int) keybind (ACS k:)
   %L (__string) LANGUAGE-lump entry (ACS l:)
   %N (int) name (ACS n:)
   %S (__string)
   %f (__real)
   %i (int)
   %p (void const *)
  Supported operations:
   __printf_bold -> void
   __printf_error -> void
   __printf_hud (int, int, int, __real, __real, __real, ...) -> void
   __printf_hud_bold (int, int, int, __real, __real, __real, ...) -> void
   __printf_log -> void
   __printf_print -> void
   __printf_string -> __string
    "__printf" [ "<" identifier ">" ] "(" string [ "," expression ]... ")"

operator return
  Returns a value. Yields void.
    "return" expression.

operator script
  Defines a script. Yields a pointer to the script. Creates a new context for
  expression based on current context. The first form makes a script that can be
  exposed globally using extern.
    "__extscript" name [ type [ flag... ] ] arglist_return
      [ "@" expression_single ] expression_single
    "__script" name [ type [ flag... ] ] arglist_return
      [ "@" expression_single ] expression_single

operator switch
  Performs a jump into a jump table. Yields void. Creates a new context for
  expression based on current context and a new context for block based on
  expression context.
    "switch" "(" expression ")" "{" [ expression ";" ]... "}"

operator struct
  Defines a struct. Yields void.
    "struct" [ name ] [ "{" [ type name ";" ]... "}" ]

operator symbol
  Creates a literal out of a low-level symbol. Yields that literal. This is
  meant for "system" headers and is not normally useful in user code.
    "__symbol" "(" type "," string ")"

operator typedef
  Defines a type. Yields void.
    "typedef" type name

operator union
  Defines a union. Yields void.
    "union" [ name ] [ "{" [ type name ";" ]... "}" ]

operator variable
  Declares a variable. Yields the variable. The first form makes a variable that
  can be exposed globally using extern.
    "__extvar"   storage_class type name [ "@" integer ]
    "__variable" storage_class type name [ "@" integer ]

operator void
  Drops the result of expression. Yields void.
    "void" expression

operator while
  Performs iteration. Yields void. Creates a new context for condition based on
  current context and a new context for remaining expression based on condition
  context.
    "while" "(" expression ")" expression_single



storage-class auto
  Allocates storage on the stack in addressable space.
    "auto"

storage-class register
  Allocates storage in registers. This is the storage class of ACS non-array
  variables defined inside a script or function.
    "register"

storage-class register-global
  Allocates storage in global-scope registers. This is the storage class of ACS
  non-array variables defined as global.
    "__globalregister"

storage-class register-map
  Allocates storage in map-scope registers. This is the storage class of ACS
  non-array variables defined outside any script or function.
    "__mapregister"

storage-class register-world
  Allocates storage in world-scope registers. This is the storage class of ACS
  non-array variables defined as world.
    "__worldregister"

storage-class registerarray-global
  Allocates storage in global-scope registerarrays. This is the storage class of
  ACS array variables defined as global.
    "__globalarray"

storage-class registerarray-map
  Allocates storage in map-scope registerarrays. This is the storage class of
  ACS array variables defined outside any script or function.
    "__maparray"

storage-class registerarray-world
  Allocates storage in world-scope registerarrays. This is the storage class of
  ACS array variables defined as world.
    "__worldarray"

storage-class static
  Allocates storage in addressable space.
    "static"



These are the types in DS.

type array
  An array of type. If multiple sizes are specified, they are applied to the
  type in reverse order. A size of 0 is legal and makes a 0-length array.
    type "[" expression "]" [ "[" expression "]" ]...

type assembly function
  A pointer type used for assembly functions. Can only be used at compile-time.
    "__asmfunc_t" arglist_return

type block
    "__block" "{" [ type ]... "}"

type char
    "char"

type const
    type "const"

type enum
    "enum" identifier
    "enum" [ identifier ]
      "{" [ identifier [ "=" expression ] [ ","
            identifier [ "=" expression ] ]... ] [ "," ] "}"

type function
  A pointer type used for functions.
    "__func_t" arglist_return

type hard bool
  A boolean type that is guaranteed to always be 0 (false) or 1 (true).
    "bool"

type int
  An integer type.
    "int"

type label
  A pointer type used for labeled expressions.
    "__label"

type line special
  A pointer type used for line specials.
    "__lnspec_t" arglist_return

type native
  A pointer type used for native/builtin functions.
    "__native_t" arglist_return

type pointer
  A pointer type used for addressable memory.
    type "*"

type real
  A real type that may be fixed-point or floating-point, based on the target.
    "__real"

type script
  A pointer type used for scripts.
    "__script_t" arglist_return

type soft bool
  A boolean type that has indeterminate representation. Proper casting, however,
  will always yield 0 (false) or 1 (true).
    "__softbool"

type string
  A pointer type used when the target needs special handling of strings.
  Consequently, its semantics are highly dependent on the target and cannot be
  relied upon portably. If the target does not require special handling, it may
  be equivalent to char * or variant.
    "__string"

type struct
    "struct" identifier [ "{" [ type identifier ";" ]... "}" ]

type union
    "union" identifier [ "{" [ type identifier ";" ]... "}" ]

type void
    "void"

